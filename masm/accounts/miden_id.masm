use.miden::account
use.miden::note
use.std::sys

const.ALREADY_INITIALIZED="Already Initialized"
const.OWNER_SUFFIX_MISMATCH="Owner suffix mismatch"
const.OWNER_PREFIX_MISMATCH="Owner prefix mismatch"
const.NAME_ALREADY_REGISTERED="Name already registered"
const.ACCOUNT_ALREADY_HAS_NAME="Account already has name"

# Storage
# 0: Initialized flag
# 1: Owner
# 2: Payment Token
# 3: Name -> ID map
# 4: ID -> Name map

# Input: [token_prefix, token_suffix]
# Output: []
export.init
    exec._assert_initialized
    # [token_prefix, token_suffix]
    exec._set_initialized
    # [token_prefix, token_suffix]
    exec.note::get_sender
    # [owner_prefix, owner_suffix, token_prefix, token_suffix]
    exec._update_owner
    # [token_prefix, token_suffix]
    exec._set_payment_token
end

# Input: [NAME]
# Output: []
export.register_name
    dupw
    # [NAME, NAME]
    exec._assert_name_not_registered
    # [NAME]
    exec.note::get_sender
    # [prefix, suffix, NAME]
    exec._assert_not_have_name
    # [NAME]
    exec.note::get_sender
    # [prefix, suffix, NAME]
    exec._update_registry
    # []
end


# Input: [NAME]
# Output: [1 or 0, VALUE, ZERO_WORD]
export.is_name_registered
    push.3
    # [3, NAME]
    exec.account::get_map_item
    # [VALUE]
    push.0.0.0.0
    # [VALUE, ZERO_WORD]
    eqw
    # [0 or 1, VALUE, ZERO_WORD]
end

# Input: [addr_prefix, addr_suffix]
# Output: [NAME]
export.get_name
    exec._get_name_by_addr
end

# Input: [addr_prefix, addr_suffix]
# Output: [1 or 0, ZERO_WORD, NAME]
export.has_name
    exec._get_name_by_addr
    # [NAME]
    padw
    # [ZERO_WORD, NAME]
    eqw
end

# Input: [prefix, suffix, NAME]
# Output: []
proc._update_registry
    push.0.0
    # [ADDR, NAME]
    dupw.1
    # [NAME, ADDR, NAME]
    dupw.1
    # [ADDR, NAME, ADDR, NAME]
    push.4
    # [4, ADDR, NAME, ADDR, NAME]
    exec.account::set_map_item
    # [OLD_ROOT, OLD_VAL, ADDR, NAME]
    dropw dropw
    # [ADDR, NAME]
    swapw
    # [NAME, ADDR]
    push.3
    # [3, NAME, ADDR]
    exec.account::set_map_item
    # [OLD_ROOT, OLD_VAL]
    dropw dropw
end

# Input: [addr_prefix, addr_suffix]
# Output: [NAME]
proc._get_name_by_addr
    push.0.0
    # [0, 0, addr_prefix, addr_suffix]
    push.4
    # [4, 0, 0, addr_prefix, addr_suffix]
    exec.account::get_map_item
    # [VALUE]
end

# Input: [NAME]
# Output: []
proc._assert_name_not_registered
    push.3
    # [3, NAME]
    exec.account::get_map_item
    # [VALUE]
    push.0.0.0.0
    # [VALUE, ZERO_WORD]
    eqw
    # [0 or 1, VALUE, ZERO_WORD]
    assertz.err=NAME_ALREADY_REGISTERED
    # [VALUE, ZERO_WORD]
    dropw dropw
    # []
end

# Input: [prefix, suffix]
# Output: []
proc._assert_not_have_name
    push.0.0
    push.4
    exec.account::get_map_item
    # [VALUE]
    padw
    eqw
    assertz.err=ACCOUNT_ALREADY_HAS_NAME
    dropw dropw
end


# Input: []
# Output: []
proc._assert_initialized
    push.0
    # [0]
    exec.account::get_item
    # [INITIALIZED_FLAG]
    drop drop drop
    # [initialized_flag]
    assertz.err=ALREADY_INITIALIZED
    # []
end

# Input: []
# Output: []
proc._set_initialized
    push.0.0.0.1
    # [0, 0, 0, 1]
    push.0
    # [0, 0, 0, 0, 1]
    debug.stack
    exec.account::set_item
    # [OLD_WORD]
    dropw
end

# Input: [owner_prefix, owner_suffix]
# Output: []
proc._update_owner
    push.0.0
    # [0, 0, owner_prefix, owner_suffix]
    push.1
    # [1, 0, 0, owner_prefix, owner_suffix]
    debug.stack
    exec.account::set_item
    # [OLD_VALUE]
    dropw
end

# Input: []
# Output: []
proc._assert_only_owner
    exec.note::get_sender
    # [sender_prefix, sender_suffix]
    push.1
    # [1, sender_prefix, sender_suffix]
    exec.account::get_item
    # [OWNER_WORD, sender_prefix, sender_suffix]
    drop drop
    # [owner_prefix, owner_suffix, sender_prefix, sender_suffix]
    swap.4
    # [sender_suffix, owner_suffix, sender_prefix, owner_prefix]
    assert_eq.err=OWNER_SUFFIX_MISMATCH
    assert_eq.err=OWNER_PREFIX_MISMATCH
    # []
end

# Input: [token_prefix, token_suffix]
# Output: []
proc._set_payment_token
    push.0.0
    # [0, 0, token_prefix, token_suffix]
    push.2
    # [2, 0, 0, token_prefix, token_suffix]
    exec.account::set_item
    dropw
end